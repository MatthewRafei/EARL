#+TITLE: EARL Lanugage Reference
#+DESCRIPTION: The "ins" and "outs" of EARL
#+AUTHOR: Zachary Haskins (malloc-nbytes)
#+EMAIL: zdhdev@yahoo.com
#+SETUPFILE: https://fniessen.github.io/org-html-themes/org/theme-readtheorg.setup
#+OPTIONS: num:nil

*NOTE*:

1. =This text format represents keywords=
2. /This test format represents a type of grammar/
3. <text> represents an identifier
4. | represents OR
5. [text] means list
6. *(text) either 0 or more
7. ?(text) means 0 or 1
8. "text" represents a string

* Your First Program

To jump right into testing out EARL, do the following to implement a /Hello World!/ program.

Create a new file called =hello.earl=

#+begin_example
print("Hello World!");
#+end_example

Then run

#+begin_src bash
  earl ./hello.earl
#+end_src

Output:

#+begin_example
Hello World!
#+end_example

* Variable Declarations
=let= <id> = /expr/;

* Mutation
<id> | /expr/ = /expr/;

* Conditionals
=if= /expr/ *{* [stmt] *}*

=else if= /expr/ *{* [stmt] *}*

=else= *{* [stmt] *}*

* TODO Pattern Matching
#+begin_quote
Pattern matching allows you to match on some pattern. This is
the case for all primitives =int=, =str=, =list= etc.

A =match= consists of a two things.
1. The pattern \rightarrow this comes right after the =match= keyword
2. Branches \rightarrow these are all the different patterns can be

Each branch can either specify one pattern or multiple patterns with the =|= (or) operator.
The =when= keyword acts as a /guard/ for the branch. If the pattern matches, only then is the
/guard/ evaluated. If the /guard/ is true, only then will the branch activate.

And finally, the base case consists of the pattern =_=. If none of the other branches *above* this
one happens, this one is guaranteed to happen (unless there is a guard).
#+end_quote

Grammar TODO

** Examples

#+begin_example
let x = 4;
let msg = "foo";

match x {
    1 -> { print("x is 1"); }
    2 | 3 -> { print("x is two or three"); }
    [1,2,3] when type(x) == "list" -> { print(x is a list and is [1,2,3]") };
    4 when msg = "bar" -> { print("x is 4 with msg = bar"); }
    4 when msg = "foo" -> { print("x is 4 with msg = foo"); }
    _ when msg = "hello" -> { print("base case, 4 is something else and msg = hello"); }
    _ -> { print("base case, 4 is something else"); }
}
#+end_example

Output:

#+begin_example
x is 4 with msg = foo
#+end_example

#+begin_example
let x = some(4);

match x {
    none -> {
        print("x is none");
    }
    some(3) -> {
        print("x is some 3");
    }
    some(k) when k % 2 == 1 -> {
        print("x is something and is odd");
    }
    some(k) -> {
        # This branch will execute.
        # `k` is the unwrapped value of `x`.
        print("x is: ", k);
        print(x.unwrap() == k);
    }
    some(_) -> {
        print("x is something");
    }
    _ -> {
        print("x is something else");
    }
}
#+end_example

Output:

#+begin_example
x is: 4
true
#+end_example

* While Loops
=while= /expr/ *{* [stmt] *}*

* For Loops

#+begin_quote
As of now, the =for= loop only enumerates by incrementing
by 1. It takes a start ($s$) and an end ($e$) and will not end until
$s = e$.

\[
\sum_{i = start}^{i < end} i = i + 1
\]

You can modify =i= within the body of the loop.

#+end_quote


=for= <id> =in= /expr/ .. /expr/ *{* [stmt] *}*

* Function Definitions

#+begin_quote
This is how to create a function. See [[Attributes]] for a
description on /attributes/.
#+end_quote

** Grammar

*( =@= [ =pub= | =world= ])

=fn= <id> *(* [ * ( =@ref= ) <id> ] *)* *{* [stmt] *}*

** Examples

#+begin_example
@world @pub
fn func1(@ref x, y, @ref z) {
    ...
}

fn func2() {
    ...
}
#+end_example

* Statement Expressions

#+begin_quote
Statement expressions are statements that are only evaluated
for their side effects such as printing, file IO, or in-place mutation
such as =List.rev=.
#+end_quote

** Grammar

<id> *( =(= /expr/ =)= | =[= /expr/ =]= | =.= <ident> ) =;=

** Examples

#+begin_example
print("hello world");
my_list.rev();
#+end_example

* Imports

#+begin_quote
EARL looks for the files relative to the directory
that it was invoked in. During runtime, if an =import=
statement is hit, it will immediately interpret that file.
If there is no processing in the =@world= scope, only the
function definitions are gathered.
#+end_quote

** Grammar

=import= "filepath"

** Examples

#+begin_example
import "std/list.earl"
import "std/math.earl"
import "std/utils.earl"
#+end_example

* Modules

#+begin_quote
/Modules/ allow other files to access resources to the
file that has the module declared in it. If a file does
not have it, the entire file is private. While modules
do allow other files to access resources, only resources
that have the =@pub= attribute.

*It is expected to have the module declaration at the top of the file.
If you do not follow this rule, undefined behavior will ocurr.*
#+end_quote

** Grammar

=mod= <id>

** Examples

file: =my-math.earl=
#+begin_example
mod MyMath

fn sum(a, b) { return a+b; }
#+end_example

file: =main.earl=
#+begin_example
mod Main

import "my-math.earl"

let s = MyMath.sum(1, 2);
#+end_example

* Attributes

#+begin_quote
Attributes embeds meta information into variables
and functions. You can have none, one, or multiple.

| Name     | Vars          | Funcs | Desc                                        |
|----------+---------------+-------+---------------------------------------------|
| =@pub=   | UNIMPLEMENTED | YES   | Makes the function available to other files |
| =@world= | NO            | YES   | The function closes in the world scope      |
| =@ref=   | YES           | NO    | Declares the var to be a reference          |

*NOTE:* =@ref= can be used as a function parameter and that function will
take a reference to the value passed to it.
#+end_quote

** Grammar

=@= *(pub | world | ref)

** Examples

#+begin_example
@world @pub
fn test_world() {
    # `i` will print since it is in the world scope.
    # The function is also marked as public so other
    # modules can have access to this function.
    print(i)
}

@pub
fn take_ref(@ref ptr) {
    ptr = ptr + 1;
}

let i = 1;
test_world();

@ref let ptr = i;
ptr = ptr + 1;

assert(ptr == 2 && i == 2);

take_ref(i);

assert(i == 3);

#+end_example

* Classes

#+begin_quote
While classes are available, support for inheritance is not yet implemented.
#+end_quote

** Grammar

=@= *(pub | world | ref)

=class= <id> ?(=[= <arg1>, <arg2>,..., <argN> =]=) ={=

    =@= *(pub | world | ref) <member1> = <id> | /expr/ =;=

    =@= *(pub | world | ref) <member2> = <id> | /expr/ =;=

    <methods>

=}=

** Examples

#+begin_example
# This class takes no arguments for the
# constructors and is also not public,
# so other modules cannot use this class.

class NoPassesArgs {
    x = 1;
    y = 2;

    @pub fn print() {
        The use of `this` keyword is optional
        print(this.x, y);
    }
}
#+end_example

#+begin_example
@pub class Point [x, y, name] {
    x = x;
    y = y;
    name = name;

    # Because this is public, you can access
    # it directly from outside the class.
    @pub desc = "A point class"

    @pub set_x(newx) {
        this.x = newx
    }

    @pub dump() {
        print(this.x);
        print(this.y);
        print(this.name);
        print(this.desc);
    }
}
#+end_example

#+begin_example
@pub class Stack [init] {
    # You could just assign `init` to `lst` here,
    # but for this example, this is done in the constructor.
    lst = [];
    sz = len(lst);

    # The constructor gets called
    # right after class instantiation.
    fn constructor() {
        for i in 0..len(init) {
            this.push(init[i]);
        }
    }

    @pub fn push(x) {
        this.lst.append(x);
        this.sz = this.sz+1;
    }

    @pub fn pop() {
        this.lst.pop(this.sz-1);
        this.sz = this.sz-1;
    }
}
#+end_example

* Datatypes

#+begin_quote
The following is a list of the currently implemented primitive types.
#+end_quote

** =int=

#+begin_quote
Your basic 32 bit integer datatype.

*Note*: They *do* over/under-flow
#+end_quote

** =bool=

#+begin_quote
The boolean datatype. Can either be =true= or =false=.
#+end_quote

** =char=

#+begin_quote
A single character. They are surrounded by single quotes ='=.
#+end_quote

** =str=

#+begin_quote
A string is a list of =char='s. They are surrounded with double quotes ="=.
#+end_quote

** =list=

#+begin_quote
A list is a growable array. They can hold any datatype, and you
can even mix multiple datatypes in a single list.
#+end_quote

** =option=

#+begin_quote
Because all variables need to be initialized when using a `let` statement,
it may be annoying to be forced to give it some initial value. The =none= type
allows you to set a variable to it and can be reassigned later.

To assign a value to a none-type, you must wrap it in =some= i.e.,

=let x = none;=

=x = some(9);=

*Note*: A lot of the functionality of the language will break if you use =none=
as another datatype before giving it an actual value.
#+end_quote

** =file=

#+begin_quote
The =file= type is a file handler. It handles opening, closing, reading, and writing
to files.

*Note*: It is up to the user to =close()= the file handle.
#+end_quote

** =unit=

#+begin_quote
Currently not being used in EARL, only in the interpreter. This type
gets returned when doing things such as side effects, non-returning function calls, etc.
#+end_quote

* Intrinsics

#+begin_quote
Intrinsics are functions that are automatically built into the interpreter.
You do not need to import anything to use these functions.
#+end_quote

** =print=

*** =print(arg1: any, arg2: any, ..., argN: any) -> unit=

#+begin_quote
Will print all elements if it can. Adds a newline at the end and flushes the output.
#+end_quote

** =assert=

*** =assert(arg1: any, arg2: any, ..., argN: any) -> unit=

#+begin_quote
Tests each argument for =1=. If any of the conditions fails,
the program immediately crashes.
#+end_quote

** =len=

*** =len(arg: list|str) -> int=

#+begin_quote
Expects either a =list= or =string=. Will give the length
as an integer.
#+end_quote

** =some(arg: any) -> option<any>=

#+begin_quote
Wraps =arg= in an =option= type.
#+end_quote

** =type(arg: any) -> str=

#+begin_quote
Returns the type of =arg= as a =str=.
#+end_quote

** =open(fp: str, mode: str) -> file=

#+begin_quote
Opens the filepath =fp= with mode =mode=. Mode must either be
=r= for read or =w= for write.
#+end_quote

** =unimplemented(arg1, arg2, ..., argN)=

#+begin_quote
Prints arg1..arg$N$ to =stderr= and exits with non-zero exit code.
#+end_quote

* Member Intrinsics

#+begin_quote
Member intrinsics are the same as intrinsics, except they are
member functions (or methods) that types have built into them.
You access these with the dot `.` notation i.e.,

=let lst = [1,2,3];=

=lst.rev();=
#+end_quote

** =list= Implementes

*** =rev() -> unit=

#+begin_quote
Reverses the list inplace.
#+end_quote

*** =append(arg1: any, arg2: any, ..., argN: any) -> unit=

#+begin_quote
Will append arg1..arg$N$ to the list.
#+end_quote

*** =pop(idx: int) -> unit=

#+begin_quote
Will remove the element at index =idx=.
#+end_quote

** =str= Implements

*** =split(delim: str) -> list=

#+begin_quote
Split a string by the delimiter =delim=.
#+end_quote

*** =pop(idx: int) -> unit=

#+begin_quote
Will remove the element at index =idx=.
#+end_quote

** =char= Implements

*** =ascii() -> int=

#+begin_quote
Returns the ascii code of the character.
#+end_quote

** =option= Implements

*** =is_none() -> bool=

#+begin_quote
Returns =true= if the value is =none=, =false= otherwise.
#+end_quote

*** =is_some() -> bool=

#+begin_quote
Returns =true= if the value is =some=, =false= otherwise.
#+end_quote

** =file= Implements

*** =close() -> unit=

#+begin_quote
Closes an opened file.
#+end_quote

*** =read() -> str=

#+begin_quote
Get the contents of a file as a =str=.
#+end_quote

*** =dump() -> unit=

#+begin_quote
Print the contents of a file.
#+end_quote

* StdLib

#+begin_quote
Upon installing EARL, the standard library is installed into
=/usr/local/include/EARL/std=. The first place that EARL looks
when evaluating an =import= statement is =/usr/local/include/EARL/=.
This means that all you need to do to import these files is do:
=import "std/<file>.earl"=.
#+end_quote

** Stack

*** *Import*: ="std/stack.earl"=

*** *Module*: =Stack=

*** *Class List*:

*** =T [init: list] -> Stack.T=

#+begin_quote
Creates a =Stack= container with the initializer list =init=.
#+end_quote

*** Implements

=push(elem: any) -> unit=

#+begin_quote
Inserts =elem= onto the top of the stack.
#+end_quote

=pop() -> unit=

#+begin_quote
Removes the most recently inserted element.
#+end_quote

=top() -> any=

#+begin_quote
Get the most recently added item.
#+end_quote

=empty() -> bool=

#+begin_quote
Check if the stack is empty
#+end_quote

*** *Function List*: /None/

*** *Examples*

#+begin_example
let s = Stack.T([]);

for i in 0..10 {
    s.push(i);
}

while s.empty() != true {
    print(s.top());
    s.pop();
}
#+end_example

#+begin_example
9
8
7
6
5
4
3
2
1
#+end_example

** Set

*** *Import*: ="std/set.earl"=

*** *Module*: =Set=

*** *Class List*:

*** =T [init: list] -> Set.T=

*Note:* The =set= currently only works with integers.

#+begin_quote
Creates a =Set= container with the initializer list =init=.
#+end_quote

*** Implements

=insert(elem: any) -> unit=

#+begin_quote
Inserts =elem= into the =set=.
#+end_quote

=contains(elem: any) -> bool=

#+begin_quote
Check if =elem= is in the set.
#+end_quote

=dump() -> unit=

#+begin_quote
Prints the contents of the =set=.
#+end_quote

*** *Function List*: /None/

*** TODO *Examples*

** List

*** *Import*: ="std/list.earl"=

*** *Module*: =List=

*** *Class List*: /None/

*** *Function List*:

*** =preset(elem: any, cap: int) -> list=

#+begin_quote
Create a list filled with =elem= of length =cap=.
#+end_quote

*** =from_range(start: int, stop: int, stride: int) -> list[int]=

#+begin_quote
Create a list filled with the numbers from =start= to =stop= (inclusive)
and increment the number by =stride=.
#+end_quote

*** =fill(@ref lst: list, k: any) -> unit=

#+begin_quote
Fill the list =lst= with element =k=.
#+end_quote

*** =sum(@ref lst: list[int]) -> int=

#+begin_quote
Sum up all elements of the list =lst=.
#+end_quote

*** =find(@ref lst: list, @ref elem: any) -> int=

#+begin_quote
Return the index that =elem= appears in =lst=. Returns =-1=
if it does not exist.
#+end_quote

*** =count(@ref lst: list, @ref elem: any) -> int=

#+begin_quote
Count the number of times that =elem= appears in =lst=.
#+end_quote

** IO

*** *Import*: ="std/io.earl"=

*** *Module*: =IO=

*** *Class List*: /None/

*** *Function List*:

*** =file_to_str(fp: str) -> str=

#+begin_quote
Opens the file at the filepath =fp= in read (=r=) mode. Returns
the contents of the file as a =str= and closes the file.
#+end_quote

** Math

*** *Import*: ="std/math.earl"=

*** *Module*: =Math=

*** *Class List*: /None/

*** *Function List*:

*** =max(x: int, y: int) -> int=

#+begin_quote
Returns the max between =x= and =y=.
#+end_quote

*** =min(x: int, y: int) -> int=

#+begin_quote
Returns the min between =x= and =y=.
#+end_quote

*** =abs(x: int) ->= $\mathbb{Z}^+$

#+begin_quote
Returns the absolute value of =x=.
#+end_quote

** Utils

*** *Import*: ="std/utils.earl"=

*** *Module*: =Utils=

*** *Class List*: /None/

*** *Function List*:

*** =is_even(n: int) -> bool=

#+begin_quote
Returns =true= if =n= is even, =false= if odd.
#+end_quote

*** =swap(@ref x1, @ref x2) -> unit=

#+begin_quote
Swaps the values of =x1= and =x2=.
#+end_quote
